buildscript {
	repositories {
		mavenLocal()
		mavenCentral()
	}

	dependencies {
		classpath "com.prezi.spaghetti:gradle-spaghetti-typescript-plugin:2.1-alpha-2-SNAPSHOT"
		classpath "com.prezi.spaghetti:gradle-spaghetti-haxe-plugin:2.1-alpha-2-SNAPSHOT"
	}
}

allprojects {
	apply plugin: "base"
}

project(":greeter-module") {
	apply plugin: "spaghetti-typescript"
}

project(":adder-module") {
        apply plugin: "spaghetti"
        
	spaghetti {
		language "kotlin"
	}
        
	task compileKotlin(type: Exec) {
		dependsOn generateHeaders
		inputs.dir file("src/main/kotlin")
		def outputFile = file("$buildDir/Adder.js")
		ext.outputFile = outputFile
		outputs.file outputFile
                workingDir project.rootDir
		commandLine "${project.rootDir}/kotlinc-js.sh", "adder-module", "Adder"
	}

	task bundleModule(type: com.prezi.spaghetti.gradle.BundleModule) {
		dependsOn compileKotlin
		inputFile compileKotlin.outputFile
	}

	task zipModule(type: Zip) {
		dependsOn bundleModule
		from bundleModule.outputDirectory
	}

	artifacts {
		modules(zipModule)
	}
}

project(":runner-module") {
	apply plugin: "spaghetti-haxe"

	haxe {
		include "com.example.runner"
	}

	dependencies {
		modules project(path: ":greeter-module", configuration: "modules"),	
		        project(path: ":adder-module", configuration: "modules")
	}	
}

apply plugin: "spaghetti"

dependencies {
	modules project(path: ":runner-module", configuration: "modules")		
}	

task packageApplication(type: com.prezi.spaghetti.gradle.PackageApplication) {
	mainModule "com.example.runner"
	execute true
	type "node"
}

task run(type: Exec) {
	dependsOn packageApplication
	commandLine "node", "${packageApplication.outputDirectory}/application.js"
}

task wrapper(type: Wrapper) {
	gradleVersion "2.1"
}
